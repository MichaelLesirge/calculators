:"SOURCE CODE FOR TWO PLAYER PONG GAME FOR TI-84 GRAPHING CALCULATOR
:"This version was entirely created by me

:"CONSTANTS

3→E :"TicksBetweenBallMovement

03→A :"L Size
03→B :"R Size

26→N :"ScreenWidth
10→M :"ScreenHeight

10→S :"WinningScore

1→G :"WallOffset

:"START

ClrHome

Disp "Loading...
Disp "
Disp "Player 1 (Left) uses '7' and '1'
Disp "Player 2 (Right) uses '9' and '4'
Disp "
Disp "Score displayed at top of screen
Disp "First to "+toString(S)+" wins"

Wait 0.1

:"MATCH INIT
0→C :"L Score
0→D :"R Score

:"MATCH LOOP

While (C<S) and (D<S)

ClrHome

:"-INIT ROUND

(N/2)→X :"Ball X
(M/2)→Y :"Ball Y 
(randInt(0,1)*2-1)→H :"Ball dX
(randInt(0,1)*2-1)→V :"Ball dY

If W=1
1→H

If W=2
­1→H

round((M-A)/2,0)→L :"L Player Y
round((M-B)/2,0)→R :"R Player Y

0→W :"Winner
0→T :"Ticks

:"-INIT DRAW

For(I,0,A-1
Output(L+I,1+G,"|
End

For(I,0,B-1
Output(R+I,N-G,"|
End

:"-ROUND LOOP
While W=0

:"--PLAYER ACTION
getKey→K

If K=72 and L>1:Then
Output(L+A-1,1+G," 
L-1→L
Output(L,1+G,"|
End

If K=92 and L<M-A+1:Then
Output(L,1+G," 
L+1→L
Output(L+A-1,1+G,"|
End

If K=74 and R>1:Then
Output(R+B-1,N-G," 
R-1→R
Output(R,N-G,"|
End

If K=94 and R<M-B+1:Then
Output(R,N-G," 
R+1→R
Output(R+B-1,N-G,"|
End

:"--WIN DETECTION
If X=1:Then
2→W
D+1→D
End

If X=N:Then
1→W
C+1→C
End

:"--SCORE BOARD
Output(1,1+4,C
Output(1,N-4,D
:"Output(1,1+4,toString(X)+" 
:"Output(1,N-4,toString(Y)+" 

:"--BALL UPDATE
If remainder(T,max(round(E-(T/100),0),1))=0 and W=0:Then

:"--BALL BOUNCE TOP/BOTTOM
If (Y≤1 or Y≥M)
­V→V

:"--BALL BOUNCE PLAYER

If ((X+H=1+G and Y≥L and Y<L+A) or (X+H=N-G and Y≥R and Y<R+B))
­H→H

:"--BALL UPDATE AND DRAW
Output(Y,X," 

:"prevent intersection
If 0=((X+H=1+G and Y+V≥L and Y+V<L+A) or (X+H=N-G and Y+V≥R and Y+V<R+B))
X+H→X

Y+V→Y
Output(Y,X,"O

End

T+1→T
End

Output(M/2-1,int((N-20)/2)+1,"Player "+toString(W)+" Wins Round!"
Output(M/2+1,int((N-5)/2)+1,toString(C)+" - "+toString(D)

0→T
getKey→K
While (T<150 and K≠83 and K≠105)
T+1→T
getKey→K
End

End

ClrHome

:"--REDRAW SCORE BOARD
Output(1,1+4,C
Output(1,N-4,D

Output(M/2-2,int((N-10)/2)+1,"Game Over!
Output(M/2+2,int((N-14)/2)+1,"Player "+toString(W)+" Wins!
Output(M/2,int((N-22)/2)+1,"Press 5 to play again.

getKey→K
While K=0
getKey→K
End

If K=83
prgmPONG2

ClrHome
Disp "PONG by ML